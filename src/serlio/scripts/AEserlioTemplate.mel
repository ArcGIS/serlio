/**
 * Serlio - Esri CityEngine Plugin for Autodesk Maya
 *
 * See https://github.com/esri/serlio for build and usage instructions.
 *
 * Copyright (c) 2012-2022 Esri R&D Center Zurich
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

proc string prtGetNodeName(string $attr) {
	return `match "^[^\.]*" $attr`;
}

proc string prtRemoveNodeName(string $attr) {
	return `substitute "^[^.]*\\." $attr ""`;  
}

proc string prtControlName(string $attr) {
	return substituteAllString(`prtRemoveNodeName($attr)`, ".", "_");
}

proc string niceName(string $longName) {
	return `attributeName -nice $longName`;
}
  
proc int prtIsStringAttr(string $attr){
	string $datatype[] = `addAttr -query -dataType $attr`;
	return `strcmp "string" $datatype[0]` == 0;
}

proc int prtIsFileAttr(string $attr){
	return `addAttr -q -uaf ($attr)`;
}

proc int prtIsColorAttr(string $attr){
	return `addAttr -q -uac ($attr)`;
}

proc int prtIsEnumAttr(string $attr){
	return (`addAttr -q -at ($attr)` == "enum");
}

proc int prtAttributeExists(string $attr){
	string $node = plugNode($attr);
	string $attributeName = plugAttr($attr);
	return`attributeExists $attributeName $node`;
}

proc int prtControlExists(string $control){
	return `control -exists $control`;
}

proc prtSetVisibility(int $visible, string $control){
	control -edit -visible $visible $control;
}

proc prtSetBackgroundColor(int $enabled, float $grayScaleColor, string $control){
	control -edit -bgc $grayScaleColor $grayScaleColor $grayScaleColor -ebg $enabled $control;
}

global proc prtReloadRPK(string $attr) {
	$node = prtGetNodeName($attr);
	$attrCurrentRule = $node + ".currentRule_Package";
	setAttr -type "string" $attrCurrentRule "";
	dgdirty $node;
	evalDeferred "refreshEditorTemplates()";
}

global proc int prtRPKChange(string $attr, string $varname) {
	$varname = prtRemoveNodeName($varname);
	$oldpkg  = eval("$tmp=$" + $varname);
	$node    = prtGetNodeName($attr);
	$newpkg  = `getAttr ($node + ".Rule_Package")`;

	return $oldpkg == $newpkg;
}
  
global proc prtShowFileDialog(string $attr, string $filters) {
	string $file[];
	
	if(startsWith($filters, "@Directory")) {
		$file = `fileDialog2 -fm 3 -cap "Select Directory" -okc "Select"`;
	} else {
		$file = `fileDialog2 -fm 1 -cap "Select File" -ff $filters`;
	}

	if(size($file) > 0) {  
		setAttr -type "string" $attr $file[0];  
		if($attr == "Rule_Package") {
			evalDeferred "refreshEditorTemplates()";
		}
	}  
}  

proc string[] getProblems(string $node){
	string $attr = $node + ".CGAC_Problems";

	if (!prtAttributeExists($attr))
		return {};

	string $cgacProblems[] = `getAttr($attr)`;
	return $cgacProblems;
}

proc int problemsHaveErrors(string $cgacProblems[]){
	for($i = 0; $i < size($cgacProblems); $i += 3){
		if($cgacProblems[$i + 1] == "Error")
			return true;
	}
	return false;
}

global proc updateCGACProblems(string $uuid, string $window, string $rulePathLayout, string $problemLayout){
	string $nodes[] = `ls $uuid`;
	string $node = $nodes[0];
	
	string $attr = $node + ".Rule_Package";

	string $rulePath = `getAttr $attr`;
	string $cgacProblems[] = getProblems($node);

	string $title = "Problems - Node: " + $node;
	window -e -title $title $window;
	scrollField -e -text $rulePath $rulePathLayout;

	string $previousErrorItems[] = `rowColumnLayout -q -childArray $problemLayout`;
	for ($errorItem in $previousErrorItems)
		deleteUI $errorItem;

	for ($i = 0; $i < size($cgacProblems); $i += 3) {
		string $errorCount = $cgacProblems[$i];
		string $errorType = $cgacProblems[$i + 1];
		string $errorString = $cgacProblems[$i + 2];

		int $displayErrorIcon = ($errorType == "Error");
		string $icon = "caution.png";
		if ($displayErrorIcon)
			$icon = "error.png";
		
		iconTextStaticLabel -h 32 -w 28 -st "iconOnly" -i $icon -parent $problemLayout;
		scrollField -height 60 -wordWrap true -text $errorString -editable false -parent $problemLayout;
		text -label ("count: " + $errorCount) -parent $problemLayout;
	}
}

global proc prtCreateCGACProblemWindow(string $attr){
	string $node = prtGetNodeName($attr);
	string $uuids[] = `ls -uuid $node`;

	string $window = `window
		-resizeToFitChildren true
		-iconName "Serlio"
		-widthHeight 500 210`;
	
	string $scrollLayout = `scrollLayout
		-horizontalScrollBarThickness 16
		-verticalScrollBarThickness   16
		-childResizable true
		-parent $window`;

	string $ruleFileLayout = `rowLayout
		-numberOfColumns 2
		-adjustableColumn 2
		-parent $scrollLayout`;

	text -label "Rule Package:" -parent $ruleFileLayout;
	$rulePathLayout = `scrollField
		-height 40
		-wordWrap true
		-editable false
		-parent $ruleFileLayout`;

	separator -height 10 -parent $scrollLayout;

	string $problemLayout = `rowColumnLayout 
		-numberOfColumns 3
		-adjustableColumn 2
		-columnSpacing 1 5
		-columnSpacing 2 5
		-columnSpacing 3 5
		-rowSpacing 1 5
		-parent $scrollLayout`;

	updateCGACProblems($uuids[0], $window, $rulePathLayout, $problemLayout);
	showWindow $window;

	string $changeCommand = "updateCGACProblems \"" + $uuids[0] + "\" \"" + $window + "\" \"" + $rulePathLayout + "\" \"" + $problemLayout +"\"";
	string $deleteWindow = "deleteUI \"" + $window +"\"";
	scriptJob -parent $window -nodeNameChanged $node $changeCommand;
	scriptJob -parent $window -attributeChange ($node + ".Rule_Package") $changeCommand;
	scriptJob -parent $window -attributeChange ($node + ".CGAC_Problems") $changeCommand;
	scriptJob -parent $window -nodeDeleted $node $deleteWindow;
}

global proc updateCGACProblemIcon(string $attr){
	string $node = prtGetNodeName($attr);
	string $cgacProblems[] = getProblems($node);
	int $hasProblems = (size($cgacProblems) > 0);
	
	string $control = prtControlName($attr) + "_CGAC_Problems";
	if (!prtControlExists($control))
		return;

	int $displayErrorIcon = problemsHaveErrors($cgacProblems);
	string $icon = "caution.png";
	if ($displayErrorIcon)
		$icon = "error.png";

	symbolButton -e -visible $hasProblems -i $icon $control;
}

global proc prtFileBrowse(string $attr, string $varname, string $filters){
	$varname = prtRemoveNodeName($varname);
	$filters = prtRemoveNodeName($filters);
	setUITemplate -pst attributeEditorTemplate;
	rowLayout
		-nc 5 
		-adj 2
		-cw 3 25
		-cw 4 25
		-cw 5 25;
	text -label `niceName($attr)`;
	string $control = prtControlName($attr);
	textField -fileName `getAttr $attr` $control;  	
	connectControl -fileName $control $attr;

	string $node = prtGetNodeName($attr);

	symbolButton -ann "show problems" -i "caution.png" -visible false -c ("prtCreateCGACProblemWindow(\"" + $attr + "\")") ($control + "_CGAC_Problems");
	updateCGACProblemIcon($attr);

	string $changeCommand = "updateCGACProblemIcon " + $attr;
	scriptJob -rp -p $control -ac ($node + ".CGAC_Problems") $changeCommand;

	symbolButton -image "navButtonBrowse.xpm" -c ("prtShowFileDialog(\"" + $attr + "\",\"" + $filters + "\" )") ($control + "_browse");	
	symbolButton -ann "reload rule package" -image "refresh.png" -c ("prtReloadRPK(\"" + $attr + "\")") ($control + "_reload");	
	setParent ..;
	setUITemplate -ppt; 
}  
  
global proc prtFileBrowseReplace(string $attr, string $varname, string $filters){
	$filters = prtRemoveNodeName($filters);
	if(prtRPKChange($attr, $varname)) {	
		string $control = prtControlName($attr);
		connectControl $control $attr;
		symbolButton -edit -c ("prtShowFileDialog(\"" + $attr + "\",\"" + $filters + "\" )") ($control + "_browse");
		symbolButton -edit -c ("prtReloadRPK(\"" + $attr + "\")") ($control + "_reload");
		symbolButton -edit -c ("prtCreateCGACProblemWindow(\"" + $attr + "\")") ($control + "_CGAC_Problems");
		updateCGACProblemIcon($attr);
	}
}

global proc prtFileBrowseReplaceRPK(string $attr, string $varname, string $filters){
	if(prtRPKChange($attr, $varname)) {
		prtFileBrowseReplace($attr, $varname, $filters);
	} else {
		$varname = prtRemoveNodeName($varname);
		$newpkg  = `getAttr $attr`;
		evalDeferred ("$" + $varname + "=\"" + $newpkg + "\"; refreshEditorTemplates();");
	}
}

global proc prtUpdateEditHighlights(string $attr){
	if (!prtAttributeExists($attr))
		return;

	int $userSet = `getAttr ($attr + "_user_set")`;
	string $control = prtControlName($attr);

	prtSetVisibility($userSet, $control + "_user_set");
	prtSetVisibility($userSet, $control + "_force_default_reset");
	prtSetBackgroundColor($userSet, 0.32, $control + "_column_layout");
	prtSetBackgroundColor(false, 0.4, $control);
}

global proc prtForceDefault(string $attr){
	setAttr ($attr + "_force_default") true;
}

global proc redrawEnum(string $attr){
	string $control = prtControlName($attr);
	if (!prtControlExists($control))
		return;
	
	setUITemplate -pst attributeEditorTemplate;

	//remove all items in the current column layout
	deleteUI ($control + "_user_set");
	deleteUI ($control + "_force_default_reset");
	deleteUI $control;

	//add all items back to the current column layout
	iconTextStaticLabel -p ($control + "_column_layout") -st "iconOnly" -visible false -i "pencilCursor.png" -w 25 -h 25 -enable false ($control + "_user_set");

	$label = `niceName $attr`;
	attrEnumOptionMenuGrp -p ($control + "_column_layout") -label $label -attribute $attr $control;
	symbolButton -p ($control + "_column_layout") -image "undo_s.png" -visible false -annotation "reset rule attribute to default value" -c ("prtForceDefault (\"" + $attr +"\")") ($control + "_force_default_reset");
	connectControl $control $attr;

	string $changeCommand = "prtUpdateEditHighlights " + $attr;
	scriptJob -p $control -ac  ($attr + "_user_set") $changeCommand;
	prtUpdateEditHighlights($attr);
}

global proc prtAttributeControl(string $attr, string $varname){
	setUITemplate -pst attributeEditorTemplate;
	string $control = prtControlName($attr);
	int $nrOfColumns = 4;
	int $isFileAttribute = `addAttr -q -uaf ($attr)`;

	if($isFileAttribute){
		$nrOfColumns = 5;
	}

	rowColumnLayout -numberOfColumns $nrOfColumns -adjustableColumn 2 ($control + "_column_layout");

	$label = `niceName $attr`;

	iconTextStaticLabel -st "iconOnly" -visible false -i "pencilCursor.png" -w 25 -h 25 -enable false ($control + "_user_set");

	if (prtIsColorAttr($attr)){
		colorSliderGrp-label $label $control;
	} else if (prtIsEnumAttr($attr)){
		attrEnumOptionMenuGrp -label $label -attribute $attr $control;
		string $enumOptions = `addAttr -q -en $attr`;
	} else {
		attrControlGrp -label $label -attribute $attr $control;
	}

	if ($isFileAttribute){
		symbolButton -image "navButtonBrowse.xpm" -c ("prtShowFileDialog(\"" + $attr + "\",\"All Files (*.*)\" )") ($control + "_browse");
	}
	symbolButton -image "undo_s.png" -visible false -annotation "reset rule attribute to default value" -c ("prtForceDefault (\"" +  $attr +"\")") ($control + "_force_default_reset");

	if(prtIsStringAttr($attr)){
		connectControl -index 2 $control $attr;
	} else {
		connectControl $control $attr;
	}
	string $changeCommand = "prtUpdateEditHighlights " + $attr;
	scriptJob -p $control -ac ($attr + "_user_set") $changeCommand;

	setParent ..;
	setUITemplate -ppt; 
	prtUpdateEditHighlights($attr);
}  

global proc prtAttributeControlReplace(string $attr, string $varname){
	if(prtRPKChange($attr, $varname)) {
		string $control = prtControlName($attr);
		if (prtIsFileAttr($attr)){
			symbolButton -edit -c ("prtShowFileDialog(\"" + $attr + "\",\"All Files (*.*)\" )") ($control + "_browse");
		}
		if(prtIsStringAttr($attr)){
			connectControl -index 2 $control $attr;
		} else if (prtIsColorAttr($attr)){
			connectControl $control $attr;
		} else if (prtIsEnumAttr($attr)){
			attrEnumOptionMenuGrp -edit -attribute $attr $control;
			connectControl $control $attr;
		} else {
			attrControlGrp -edit -attribute $attr $control;
			connectControl $control $attr;
		}
		symbolButton -edit -c ("prtForceDefault (\"" +  $attr +"\")") ($control + "_force_default_reset");
		string $changeCommand = "prtUpdateEditHighlights " + $attr;
		scriptJob -p $control -ac ($attr + "_user_set") $changeCommand;
	}
	prtUpdateEditHighlights($attr);
}

global proc AEserlioTemplate(string $node) {
	editorTemplate -suppress "caching"; 
	editorTemplate -suppress "nodeState";

    editorTemplate -beginScrollLayout;
     
	editorTemplate -beginLayout "CGA Rules" -collapse 0;
	string $varname = ("AEprtTemplate_" + $node);
	eval ("global string $" + $varname + "=\"" + `getAttr ($node + ".Rule_Package")` + "\"");
	string $filter = niceName($node + ".Rule_Package");
	editorTemplate -callCustom "prtFileBrowse" "prtFileBrowseReplaceRPK" "Rule_Package" $varname  $filter;

	editorTemplate -l `niceName($node+".Random_Seed")` -adc "Random_Seed";

	editorTemplate -endLayout;
		
	string $attrs[] = `listAttr -ud $node`;
	string $currentGroupName = "";
	string $currentCGAFile = "";
	
	$skip = 0;
	for($attr in $attrs) {
	
		string $longNameWithNode = $node + "." + $attr;	
		string $category[] = `addAttr -q  -ct ($longNameWithNode)`;
		if (size($category)<2)
			$skip = 1;
		
		if($skip-- > 0) continue;
		
		string $cgaFile = $category[0];
		string $groupName = $category[1];
	
		if ($cgaFile != $currentCGAFile) {
			if ($currentGroupName != "") {
				editorTemplate -endLayout;
				$currentGroupName = "";
			}
			if ($currentCGAFile != "") {
				editorTemplate -endLayout;
				editorTemplate -beginLayout $cgaFile -collapse 1;
			}
			else {
				editorTemplate -beginLayout $cgaFile -collapse 0;
			}
			
			$currentCGAFile = $cgaFile;
			
		}

		if ($groupName != $currentGroupName) {
			if ($currentGroupName != "")
				editorTemplate -endLayout;
			$currentGroupName = $groupName;
			if ($currentGroupName != "")
				editorTemplate -beginLayout $currentGroupName -collapse 0;
		}
		
		
		editorTemplate -callCustom "prtAttributeControl" "prtAttributeControlReplace" $attr $varname;
		if(`addAttr -q  -uac ($longNameWithNode) `) 
			$skip = 3;
	}
		
	if ($currentGroupName != "")
		editorTemplate -endLayout;
		
	editorTemplate -endLayout;

	AEdependNodeTemplate $node;
	editorTemplate -aec;
	editorTemplate -endScrollLayout;
}