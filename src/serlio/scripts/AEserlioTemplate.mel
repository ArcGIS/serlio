/**
 * Serlio - Esri CityEngine Plugin for Autodesk Maya
 *
 * See https://github.com/esri/serlio for build and usage instructions.
 *
 * Copyright (c) 2012-2019 Esri R&D Center Zurich
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

proc string prtNode(string $attr) {
	return `match "^[^\.]*" $attr`;
}

proc string prtPlug(string $attr) {
	return `substitute "^[^.]*\\." $attr ""`;  
}

proc string prtControlName(string $attr) {
	return substituteAllString(`prtPlug($attr)`, ".", "_");
}

proc string niceName(string $longName) {
	return `attributeName -nice $longName`;
}
  
global proc prtReloadRPK(string $attr) {
	$node = prtNode($attr);
	$attrCurrentRule = $node + ".currentRule_Package";
	setAttr -type "string" $attrCurrentRule "";
	dgdirty $node;
}

global proc int prtRPKChange(string $attr, string $varname) {
	$varname = prtPlug($varname);
	$oldpkg  = eval("$tmp=$" + $varname);
	$node    = prtNode($attr);
	$newpkg  = `getAttr ($node + ".Rule_Package")`;

	return $oldpkg == $newpkg;
}
  
global proc prtShowFileDialog(string $attr) {
	$node    = prtNode($attr);
	$plug    = prtPlug($attr);  
	$filters = `attributeQuery -n $node -nn $plug`;

	string $file[];
	
	if(startsWith($filters, "@Directory")) {
		$file = `fileDialog2 -fm 3 -cap "Select Directory" -okc "Select"`;
	} else {
		$file = `fileDialog2 -fm 1 -cap "Select File" -ff $filters`;
	}

	if(size($file) > 0) {  
		setAttr -type "string" $attr $file[0];  
		if($attr == "Rule_Package") {
			evalDeferred "refreshEditorTemplates()";
		}
	}  
}  

global proc prtFileBrowse(string $attr, string $varname){
	$varname = prtPlug($varname);
	setUITemplate -pst attributeEditorTemplate;
	rowLayout -nc 4;  
	text -label `niceName($attr)`;
	string $control = prtControlName($attr);
	textField -fileName `getAttr $attr` $control;  	
	connectControl -fileName $control $attr;
	symbolButton -image "navButtonBrowse.xpm" -c ("prtShowFileDialog(\"" + $attr + "\")") ($control + "_browse");	
	symbolButton -ann "reload rule package" -image "refresh.png" -c ("prtReloadRPK(\"" + $attr + "\")") ($control + "_reload");	
	setParent ..;
	setUITemplate -ppt; 
}  
  
global proc prtFileBrowseReplace(string $attr, string $varname){
	if(prtRPKChange($attr, $varname)) {	
		string $control = prtControlName($attr);
		connectControl $control $attr;
		symbolButton -edit -c ("prtShowFileDialog(\"" + $attr + "\")") ($control + "_browse");
	}
}

global proc prtFileBrowseReplaceRPK(string $attr, string $varname){
	if(prtRPKChange($attr, $varname)) {
		prtFileBrowseReplace($attr, $varname);
	} else {
		$varname = prtPlug($varname);
		$newpkg  = `getAttr $attr`;
		evalDeferred ("$" + $varname + "=\"" + $newpkg + "\"; refreshEditorTemplates();");
	}
}

global proc prtColorChooser(string $attr, string $varname) {
	setUITemplate -pst attributeEditorTemplate;
	string $control = prtControlName($attr);
	colorSliderGrp -label `niceName($attr)` $control;
	connectControl $control $attr;
	setParent ..;
	setUITemplate -ppt; 
}  

global proc prtColorChooserReplace(string $attr, string $varname){
	if(prtRPKChange($attr, $varname)) {
		string $control = prtControlName($attr);
		connectControl $control $attr;
	}
}

global proc AEserlioTemplate(string $node) {
	editorTemplate -suppress "caching"; 
	editorTemplate -suppress "nodeState";

    editorTemplate -beginScrollLayout;
     
	editorTemplate -beginLayout "CGA Rules" -collapse 0;
	string $varname = ("AEprtTemplate_" + $node);
	eval ("global string $" + $varname + "=\"" + `getAttr ($node + ".Rule_Package")` + "\"");
	editorTemplate -callCustom "prtFileBrowse" "prtFileBrowseReplaceRPK" "Rule_Package" $varname;

	editorTemplate -l `niceName($node+".Random_Seed")` -adc "Random_Seed";

	editorTemplate -endLayout;
		
	string $attrs[] = `listAttr -ud $node`;
	string $currentGroupName = "";
	string $currentCGAFile = "";
	
	$skip = 0;
	for($attr in $attrs) {
	
		string $longNameWithNode = $node + "." + $attr;	
		string $category[] = `addAttr -q  -ct ($longNameWithNode)`;
		if (size($category)<2)
			$skip = 1;
		
		if($skip-- > 0) continue;
		
		string $cgaFile = $category[0];
		string $groupName = $category[1];
	
		if ($cgaFile != $currentCGAFile) {
			if ($currentGroupName != "") {
				editorTemplate -endLayout;
				$currentGroupName = "";
			}
			if ($currentCGAFile != "") {
				editorTemplate -endLayout;
				editorTemplate -beginLayout $cgaFile -collapse 1;
			}
			else {
				editorTemplate -beginLayout $cgaFile -collapse 0;
			}
			
			$currentCGAFile = $cgaFile;
			
		}

		if ($groupName != $currentGroupName) {
			if ($currentGroupName != "")
				editorTemplate -endLayout;
			$currentGroupName = $groupName;
			if ($currentGroupName != "")
				editorTemplate -beginLayout $currentGroupName -collapse 0;
		}
		
		
		if(`addAttr -q  -uaf ($longNameWithNode) `) {
			editorTemplate -callCustom "prtFileBrowse" "prtFileBrowseReplace" $attr $varname;
		} else if(`addAttr -q  -uac ($longNameWithNode) `) {
			editorTemplate -callCustom "prtColorChooser" "prtColorChooserReplace" $attr $varname;
			$skip = 3;
		} else {
			editorTemplate  -l `niceName($longNameWithNode)` -adc $attr;
		}
	}
		
	if ($currentGroupName != "")
		editorTemplate -endLayout;
		
	editorTemplate -endLayout;
 
 	editorTemplate -aec;

    editorTemplate -endScrollLayout;
}